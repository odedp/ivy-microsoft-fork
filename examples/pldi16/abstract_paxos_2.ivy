#lang ivy1.3

module total_order(r) = {
    axiom r(X,X)                        # Reflexivity
    axiom r(X, Y) & r(Y, Z) -> r(X, Z)  # Transitivity
    axiom r(X, Y) & r(Y, X) -> X = Y    # Anti-symmetry
    axiom r(X, Y) | r(Y, X)             # Totality
}

type node
type value
type quorum
type round

individual round0 : round
individual none: round
relation le(X:round, Y:round)
instantiate total_order(le)
axiom le(round0, X)
axiom round0 ~= none

relation member(N:node, Q:quorum)
# forall Q1 Q2 exists N. member(N, Q1) & member(N, Q2)
individual quorum_intersection(Q1:quorum, Q2:quorum) : node  # a Skolem function
axiom member(quorum_intersection(Q1, Q2), Q1)
axiom member(quorum_intersection(Q1, Q2), Q2)

relation proposal(R1:round, V:value) # 2a
relation vote(N:node, R:round, V:value) # 2b
relation decision(R:round, V:value) # got 2b from a quorum
relation rnd(N:node, R:round) # current round

# 1b messages
type msg1b_t
# message fields:
individual leader_round(M:msg1b_t) : round
individual last_round(M:msg1b_t) : round
individual last_vote(M:msg1b_t) : value
# extensionality axiom - msg1b_t is a tuple
axiom (
    leader_round(M1) = leader_round(M2) &
    last_round(M1) = last_round(M2) &
    last_vote(M1) = last_vote(M2)
) -> M1 = M2
relation msg1b(M:msg1b_t, N:node)

# ghost functions for recording the existence of quorums
individual quorum_of_proposal(R:round) : quorum
individual quorum_of_decision(R:round) : quorum
# ghost relation: the max round seen by the leader of R1 is  R2
relation round_of_proposal(R1:round,R2:round)
relation voter_of_proposal(R1:round,N:node)

init ~proposal(R1,V)
init ~vote(N,R,V)
init ~decision(R,V)
init rnd(N, R) <-> R = round0
init ~msg1b(M, N)
init ~round_of_proposal(R1,R2)
init ~voter_of_proposal(R1,N)

action cast_vote = {
  # phase 2b
  local n1:node, v1:value, r1:round {
    assume rnd(n1, r1);
    assume ~vote(n1,r1,V);
    assume proposal(r1,v1);
    vote(n1, r1, v1) := true
  }
}

action decide = {
  # get 2b from a quorum
  local v:value, q:quorum, r:round {
    assume r ~= none;
    assume member(N, q) -> vote(N, r, v);
    decision(r,v) := true;
      quorum_of_decision(r) := q
  }
}

action join_round = {
  # receive 1a and answer with 1b
  local n:node, r1:round, r2:round {
    assume r2 ~= none;
    # r1 is strictly smaller than r2:
    assume r1 ~= r2;
    assume le(r1,r2);
    # node n1 is in round r1:
    assume rnd(n,r1);
    # node n1 joins r2:
    rnd(n, R) := R = r2
  }
}

action propose = {
  local v:value, q:quorum, r1:round, r2:round, n:node {
    assume r1 ~= none;
    assume ~proposal(r1,V);
    assume member(N, q) & rnd(N,R) -> le(r1,R);

    # find the latest vote in the quorum before round r1
    if ~(member(N:node,q) & vote(N:node,R:round,V:value) & ~le(r1, R:round)) {
        assume r2 = none
    } else {
        assume ~le(r1,r2);
        assume member(N,q) & vote(N,R,V) & ~le(r1, R) -> le(R,r2);
        assume member(n,q) & vote(n,r2,v)
    };
    proposal(r1, v) := true;
    quorum_of_proposal(r1) := q;
    round_of_proposal(r1, R) := R = r2;
    voter_of_proposal(r1, V) := V = n
  }
}

export cast_vote
export join_round
export decide
export propose

# safety property:
conjecture decision(R1,V1) & decision(R2,V2) -> V1 = V2

# invariants
conjecture ~le(R2,R1) & decision(R1, V1) & vote(N2, R2, V2) -> V1 = V2
conjecture ~le(R2,R1) & decision(R1, V1) & proposal(R2, V2) -> V1 = V2

conjecture vote(N1,R,V1) & vote(N2,R,V2) -> V1 = V2
conjecture vote(N,R,V) -> proposal(R,V)
conjecture proposal(R,V1) & proposal(R,V2) -> V1 = V2

conjecture member(N,quorum_of_decision(R))& decision(R,V) -> vote(N,R,V)

conjecture proposal(R1,V) & member(N, quorum_of_proposal(R1)) & rnd(N,R2) -> le(R1,R2)

conjecture proposal(R,V) -> R ~= none

conjecture rnd(N,R1) & rnd(N,R2) -> R1 = R2
conjecture vote(N,R1,V) & rnd(N,R2) -> le(R1,R2)

conjecture round_of_proposal(R1,R2) & proposal(R1,V) & round_of_proposal(R1,R3) -> R2 = R3
conjecture voter_of_proposal(R1,N1) & proposal(R1,V) & voter_of_proposal(R1,N2) -> N1 = N2
conjecture round_of_proposal(R1,R2) & R2 ~= none -> ~le(R1,R2)

conjecture proposal(R1,V1) & member(N1,quorum_of_proposal(R1)) & round_of_proposal(R1,R2) & R2 ~= none & vote(N1,R2,V2) -> V1 = V2
conjecture proposal(R1,V1) & member(N1,quorum_of_proposal(R1)) & round_of_proposal(R1,R2) & R2 ~= none & ~le(R1,R3) & ~le(R3,R2) -> ~vote(N1,R3,V2)
conjecture proposal(R1,V1) & member(N1,quorum_of_proposal(R1)) & round_of_proposal(R1,R2) & R2 = none & ~le(R1,R3) -> ~vote(N1,R3,V2)

conjecture proposal(R1,V1) & voter_of_proposal(R1,N) & round_of_proposal(R1,R2) & R2 ~= none -> vote(N,R2,V1) & member(N, quorum_of_proposal(R1))

# conjecture proposal(R1,V1) & ~(member(N1:node,quorum_of_proposal(R1)) & vote(N1:node,R2:round,V2:value) & ~le(R1, R2)) & member(N2,quorum_of_proposal(R1)) & ~le(R1,R3) -> ~vote(N2,R3,V3)
# conjecture proposal(R,V) & member(N,quorum_of_proposal(R)) & vote(N,R,V2) -> V = V2
# conjecture proposal(R,V) & member(N,quorum_of_proposal(R)) & ~le(R2,R) -> ~vote(N,R,V2)

# counter-example in 8 steps:
# conjecture ~(decision(R1,V1) & decision(R2,V2) & R1 ~= R2)

# counter-example in 11 steps:
# conjecture ~(decision(R1,V1) & decision(R2,V2) & decision(R3,V3) & R1 ~= R2 & R3 ~= R2 & R1 ~= R3)

# choosable_at(v, b) = member(N,Q) & rnd(N,R) & le(b,R) -> vote(N,b,v)
# safe_at(v,b) = le(R,b) & choosable_at(V,R) -> V = v
# safe: conjecture vote(N,R,V) -> safe_at(V,R)
# promised(n,r1,r2,v) = (vote(n,r1,V1) -> V1 = v) & (le(r1,R) & r1 ~= R & le(R,r2) & R ~= r2 -> ~vote(n,R,V2))
# conjecture: propose(R,V) -> (\E r . r ~= R & le(r,R) & (\E q n . member(n,Q) & vote(n,r,V) & (member(N,Q) -> promised(N,r,R,V))))
